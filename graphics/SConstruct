env = Environment();

# Check if lilyUtilsAppend was correctly passed into this as an
# SConscript. This should just be the return value of the
# Lily-Engine-Utils SConscript call.
Import("lilyUtilsAppend");
if lilyUtilsAppend == None:
    print "Lily Utils lib needed!"
    Exit(1);

includeDir = [Dir("./include")];

env.Append(CPPPATH=includeDir);

# Attempt to use FreeType2 if it's available.
try:
    env.ParseConfig("pkg-config freetype2 --libs --cflags");
    env.Append(CPPDEFINES={"-DEXPOP_USE_FREETYPE2":1});
except:
    pass;

env2 = env.Clone();

env.Append(**lilyUtilsAppend);
lilyGraphicsLib = env.Library(
    "lilyenginegraphics",
    ["src/font/font.cpp",
     "src/font/font_builtin2.cpp",
     "src/font/fontrenderer.cpp",
     "src/imagegl/imagegl.cpp",
     "src/glcontext/glcontext_generated.cpp",
     "src/glcontext/glcontext.cpp",
     "src/glutils.cpp"]);

# Looks like we need to do this to copy include files over.
env.Dir("include/lilyengine");

appendValues = {
    "LIBS":[lilyGraphicsLib, env2["LIBS"], "GL", "GLU"],
    "CPPPATH":[env2["CPPPATH"]]
};
Return("appendValues");
